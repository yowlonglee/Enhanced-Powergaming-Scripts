COPY ~enhanced-powergaming-scripts/baf/upload/mocore.baf~ ~enhanced-powergaming-scripts/baf/upload/mocore.baf~

///////////////////////////////////////////////////////////////////////////
// Add Relentless Assault                                                //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// Skills and Abilities Relentless Assault~~~~~
    string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  Global("BDAI_DISABLE_DEFENSIVE","LOCALS",0)
  OR(3)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
    !EEex_LuaTrigger("return MO_AmCastingCooldown()")
  !GlobalTimerNotExpired("MO_ItemUsedTimer","LOCALS")
  OR(2)
    !StateCheck(Myself,STATE_INVISIBLE)
    !Global("BDAI_SKILL_MODE","LOCALS",2)
  HaveSpellRES("MOBTM01")
  !CheckSpellState(Myself,IMPROVED_HASTE)
  !CheckSpellState(Myself,GREATER_WHIRLWIND)
  See([EVILCUTOFF])
  OR(6)
    WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(6)
    WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(33)
    EEex_LuaTrigger("return ChallengeRating()")
    Range([EVILCUTOFF.0.GOLEM],30)
    Range([EVILCUTOFF.0.MIST],30)
    Range([EVILCUTOFF.0.KUO-TOA],30)
    Range([EVILCUTOFF.0.GENIE],30)
    Range([EVILCUTOFF.0.GITHYANKI],30)
    Range([EVILCUTOFF.0.RAKSHASA],30)
    Range([EVILCUTOFF.0.VAMPYRE],30)
    Range([EVILCUTOFF.0.MIND_FLAYER],30)
    Range([EVILCUTOFF.0.DRAGON],30)
    Range([EVILCUTOFF.0.DEMONIC],30)
    Range([EVILCUTOFF.0.BEHOLDER],30)
    Range([EVILCUTOFF.0.UMBERHULK],30)
    Range([EVILCUTOFF.0.MYCONID],30)
    Range([EVILCUTOFF.0.VAMPIRE],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.DEMILICH],30)
    Range([EVILCUTOFF.0.FAIRY],30)
    Range([EVILCUTOFF.0.0.WRAITH],30)
    Range([EVILCUTOFF.0.0.WIGHT],30)
    Range([EVILCUTOFF.0.0.SPECTRE],30)
    Range([EVILCUTOFF.0.0.WYVERN],30)
    Range([EVILCUTOFF.0.0.BASILISK],30)
    Range([EVILCUTOFF.0.SOLAR],30)
    Range([EVILCUTOFF.0.PLANATAR],30)
    Range([EVILCUTOFF.0.ANTISOLAR],30)
    Range([EVILCUTOFF.0.DARKPLANATAR],30)
    See([ENEMY.0.0.MAGE_ALL])
    See([ENEMY.0.0.BARD_ALL])
    See([ENEMY.0.0.CLERIC_ALL])
    See([ENEMY.0.0.DRUID_ALL])
    See([ENEMY.0.0.SORCERER])
    See([ENEMY.0.0.CLERIC_MAGE])
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SpellRES("MOBTM01",Myself)
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Add Relentless Assault Check for Prebuffing                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~!CheckSpellState(Myself,NEED_IMPROVED_HASTE)
  !HaveSpell(WIZARD_IMPROVED_HASTE)
  !HaveSpell(WARRIOR_GREATER_WHIRLWIND)~~~~~
    string = ~~~~~!CheckSpellState(Myself,NEED_IMPROVED_HASTE)
  !HaveSpell(WIZARD_IMPROVED_HASTE)
  !HaveSpell(WARRIOR_GREATER_WHIRLWIND)
  !HaveSpellRES("MOBTM01")~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Add Relentless Assault Check for Prebuffing                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~CheckSpellState(Myself,NEED_IMPROVED_HASTE)
  OR(2)
    HaveSpell(WIZARD_IMPROVED_HASTE)
    HaveSpell(WARRIOR_GREATER_WHIRLWIND)~~~~~
    string = ~~~~~CheckSpellState(Myself,NEED_IMPROVED_HASTE)
  OR(3)
    HaveSpell(WIZARD_IMPROVED_HASTE)
    HaveSpell(WARRIOR_GREATER_WHIRLWIND)
    HaveSpellRES("MOBTM01")~~~~~ //"
  END
