COPY ~enhanced-powergaming-scripts/baf/upload/mocore.baf~ ~enhanced-powergaming-scripts/baf/upload/mocore.baf~

///////////////////////////////////////////////////////////////////////////
// Add Mind Blank                                                        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    warn = 0
    STR_VAR
    pattern = ~~~~~// NOT HAS Mind Blank Check~~~~~
    string = ~~~~~!CheckSpellState(LastSeenBy(Myself),MIND_BLANK)~~~~~ //"
  END

  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// IWD Impervious Sanctity of Mind~~~~~
    string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !EEex_LuaTrigger("return AmMoving()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  HaveSpell(CLERIC_IMPERVIOUS_SANCTITY_OF_MIND)  // sppr735.SPL (Impervious Sanctity of Mind)
  !CheckSpellState(Myself,CHAOTIC_COMMANDS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    Spell(Myself,CLERIC_IMPERVIOUS_SANCTITY_OF_MIND)  // sppr735.SPL (Impervious Sanctity of Mind)
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Add Divine Protection and Greater Divine Protection                   //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// IWD (Greater) Divine Protection~~~~~
    string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  See([EVILCUTOFF])
  OR(9)
    Range([EVILCUTOFF.0.SOLAR],10)
    Range([EVILCUTOFF.0.PLANATAR],10)
    Range([EVILCUTOFF.0.ANTISOLAR],10)
    Range([EVILCUTOFF.0.DARKPLANATAR],10)
    Range([EVILCUTOFF.0.VAMPYRE],10)
    Range([EVILCUTOFF.0.VAMPIRE],10)
    Range([EVILCUTOFF.0.MIND_FLAYER],10)
    Range([EVILCUTOFF.0.DEMONIC],10)
    HPPercentLT(Myself,51)
  HaveSpell(CLERIC_GREATER_SHIELD_OF_LATHANDER)  // sppr737.SPL (Greater Divine Protection)
  !CheckSpellState(Myself,SHIELD_OF_LATHANDER)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    Spell(Myself,CLERIC_GREATER_SHIELD_OF_LATHANDER)  // sppr737.SPL (Greater Divine Protection)
END

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  See([EVILCUTOFF])
  OR(9)
    Range([EVILCUTOFF.0.SOLAR],10)
    Range([EVILCUTOFF.0.PLANATAR],10)
    Range([EVILCUTOFF.0.ANTISOLAR],10)
    Range([EVILCUTOFF.0.DARKPLANATAR],10)
    Range([EVILCUTOFF.0.VAMPYRE],10)
    Range([EVILCUTOFF.0.VAMPIRE],10)
    Range([EVILCUTOFF.0.MIND_FLAYER],10)
    Range([EVILCUTOFF.0.DEMONIC],10)
    HPPercentLT(Myself,51)
  HaveSpell(CLERIC_SHIELD_OF_LATHANDER)  // sppr520.SPL (Divine Protection)
  !CheckSpellState(Myself,SHIELD_OF_LATHANDER)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    Spell(Myself,CLERIC_SHIELD_OF_LATHANDER)  // sppr520.SPL (Divine Protection)
END
~~~~~ //"
  END
